apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.android.gms.strict-version-matcher-plugin'

configurations {
    all {
        exclude module: 'httpclient'
    }
}

//For CircleCi:
// base64 the keystore and keystore.properties file:
//  openssl base64 -A -in keystore.properties
//  openssl base64 -A -in adbshellkit.keystore
// add to https://circleci.com/gh/kwattsorg/adbshellkit/edit#env-vars:
//  KEYSTORE_PROPERTIES_B64 = ...  && KEYSTORE_B64 = ...
// add to circleci config.yml
//  - run: echo KEYSTORE_PROPERTIES_B64 | base64 -d > keystore.properties
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    signingConfigs {
        release {
            def keystoreFilename = keystoreProperties['storeFile']
            storeFile = keystoreFilename == null ? null : rootProject.file(keystoreFilename)
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']

        }
    }
    compileSdkVersion 28
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "net.kwatts.android.droidcommandpro"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 108
        versionName "10.8"

        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'project.properties'
        exclude 'META-INF/*'
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        disable 'GoogleAppIndexingWarning'
    }
}

dependencies {
/** File chooser */
    //implementation 'com.obsez.android.lib.filechooser:filechooser:1.1.10'
    implementation project(":android-file-chooser")


/** Kotlin */
    //https://github.com/JetBrains/kotlin/blob/master/ChangeLog.md
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.50"


/** Support */
    def googleSupportVersion = "28.0.0"
    implementation "com.android.support:customtabs:$googleSupportVersion"
    implementation "com.android.support:design:$googleSupportVersion"
    implementation "com.android.support:support-v4:$googleSupportVersion"
    implementation "com.android.support:appcompat-v7:$googleSupportVersion"
    implementation "com.android.support:cardview-v7:$googleSupportVersion"
    implementation "com.android.support:preference-v7:$googleSupportVersion"
    implementation "com.android.support:support-compat:$googleSupportVersion"
    implementation "com.android.support:support-media-compat:$googleSupportVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    def lifecycle_version = "1.1.1"
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    implementation "android.arch.lifecycle:common-java8:$lifecycle_version"


/** Firebase */
//https://firebase.google.com/support/release-notes/android
//https://developers.google.com/android/guides/releases

    //https://developers.google.com/admob/android/rel-notes
    implementation 'com.google.firebase:firebase-ads:17.2.1'
    implementation 'com.google.firebase:firebase-auth:16.1.0'
    implementation 'com.google.firebase:firebase-core:16.0.9'
    implementation 'com.google.firebase:firebase-database:16.1.0'
    //implementation 'com.google.firebase:firebase-perf:16.2.5'
    implementation 'com.google.firebase:firebase-storage:16.1.0'

    //https://firebaseopensource.com/projects/firebase/firebaseui-android/
    //implementation 'com.firebaseui:firebase-ui-database:x.x.x'


/** GMS */
    //https://developers.google.com/android/guides/releases
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'com.google.android.gms:play-services-location:16.0.0'


/** Third-party */
    //https://github.com/google/gson/blob/master/CHANGELOG.md
    implementation 'com.google.code.gson:gson:2.8.6'

    //https://github.com/afollestad/material-dialogs/releases
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'

    //https://github.com/json-path/JsonPath/releases
    implementation 'com.jayway.jsonpath:json-path:2.4.0'

    //https://github.com/DexPatcher/multidexlib2/releases
    implementation 'com.github.lanchon.dexpatcher:multidexlib2:2.2.4'
    //https://github.com/JesusFreke/smali/wiki
    implementation 'org.smali:dexlib2:2.2.4'

    //https://github.com/JakeWharton/timber/blob/master/CHANGELOG.md
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //https://github.com/flipagram/android-assetcopier
    implementation 'com.github.flipagram:android-assetcopier:0.1.1'

    //https://square.github.io/okhttp/changelog_3x/
    //https://github.com/square/okhttp/blob/master/CHANGELOG.md
    //implementation 'com.squareup.okhttp3:okhttp:x.x.x'

    //https://github.com/bumptech/glide/releases
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'

    //https://github.com/topjohnwu/libsu/blob/master/CHANGELOG.md
    def libsuVersion = '2.5.1'
    implementation "com.github.topjohnwu.libsu:core:$libsuVersion"
    implementation "com.github.topjohnwu.libsu:io:$libsuVersion"

    //https://github.com/eggheadgames/android-about-box/releases
    implementation 'com.github.eggheadgames:android-about-box:1.3.2'

    //https://docs.fabric.io/android/changelog.html#crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    //https://github.com/kevalpatel2106/android-hidden-camera/releases
    implementation 'com.kevalpatel2106:hiddencamera:1.3.5'

    //https://github.com/googlemaps/google-maps-services-java/blob/master/CHANGELOG.md
    implementation 'com.google.maps:google-maps-services:0.9.4'

    //http://www.slf4j.org/news.html
    //implementation 'org.slf4j:slf4j-simple:x.x.x'

    //https://commons.apache.org/proper/commons-io/changes-report.html
    implementation 'commons-io:commons-io:2.6'

    // Cloud
    //https://mvnrepository.com/artifact/com.google.cloud/
    //implementation 'com.google.cloud:google-cloud-xxx:x.x.x'
    //https://cloud.google.com/dlp/docs/release-notes
    //implementation 'com.google.cloud:google-cloud-dlp:x.x.x'
    //https://github.com/box/box-java-sdk/blob/master/CHANGELOG.md
    //implementation 'com.box:box-java-sdk:x.x.x'
    //https://mvnrepository.com/artifact/com.box/box-android-browse-sdk
    //implementation 'com.box:box-android-browse-sdk:x.x.x'
    //https://mvnrepository.com/artifact/com.box/box-android-sdk
    //implementation 'com.box:box-android-sdk:x.x.x'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.github.triplet.play'


// https://github.com/Triple-T/gradle-play-publisher
// run ./gradlew tasks and look under publishing
// to build and push to the alpha track: ./gradlew publishReleaseApk
play {
    track = 'alpha' // enroll at https://play.google.com/apps/internaltest/4698013762293665402
    //userFraction = 0.5
    //releaseStatus = "Latest test release"
    serviceAccountEmail = 'devdeploy@api-7203743493139905511-377866.iam.gserviceaccount.com'
    serviceAccountCredentials = file('play-devdeploy-64030c71a8cc.p12')
}
